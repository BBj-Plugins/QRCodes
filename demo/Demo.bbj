? 'HIDE'

use ::QRCodes/QRCodes.bbj::QRCodes

declare auto BBjImage img!

sg!             = BBjAPI().openSysGui("X0")

if info(3,6)="5" then
    wnd!        = sg!.addWindow(10,10,500,600,"QR Demo",$01001010$)
    top = 100
    left = BBjAPI().getSysGui().getSystemMetrics().getScreenSize().getWidth()/2-250
else
    wnd!        = sg!.addWindow(10,10,500,600,"QR Demo",$00000012$)
fi
wnd!            .setCallback(BBjAPI.ON_CLOSE,"byebye")

st!             = wnd!.addStaticText(100,left+50,top+15,400,25,"Text:","")
ed_txt!         = wnd!.addEditBox(101,left+80,top+10,280,25,"https://www.basis.com")

btn_update!     = wnd!.addButton(102,left+380,top+10,80,25,"Update")
btn_update!     .setCallback(BBjAPI.ON_BUTTON_PUSH,"show_img")

btn_png!        = wnd!.addButton(103,left+180,top+480,120,25,"Save PNG")
btn_png!        .setCallback(BBjAPI.ON_BUTTON_PUSH,"save_png")

wnd!            .setBackColor(BBjColor.WHITE)
st!             .setOpaque(false)


rem PNG to Clipboard will only work after this one was resolved:
rem https://bugzilla.basis.com/show_bug.cgi?id=32703
if rev >="REV 19.10" AND info(0,4)>="Build: 08/22/2019 17:46" then
    
    rem CLIPFROMSTR only works in GUI for now
    if info(3,6)<>"5" then
        btn_clip!   = wnd!.addButton(104,left+180,top+510,120,25,"Copy to Clipboard")
        btn_clip!   .setCallback(BBjAPI.ON_BUTTON_PUSH,"copy_clipboard")
    fi
fi

gosub show_img

wnd!.setVisible(1)

process_events

byebye:
    bye


show_img:
    string$ = ed_txt!.getText() 
    bytes! = QRCodes.getQRCode(string$,400,400,err=generation_error)
    img! = sg!.getImageManager().loadImageFromBytes(bytes!)
    if ctrl_img! <> null()then 
        ctrl_img!.destroy()
    fi
    ctrl_img! = wnd!.addImageCtrl(400,left+50,top+50,400,400,img!)
return

copy_clipboard:
     CLIPFROMSTR clipregformat("png"),bytes!
return

save_png:
    if info(3,6) = "5" then
        gosub createpngfile
        BBjAPI().getThinClient().getClientFileSystem().getClientFile("qrcode.png").copyToClient(filename$)
        erase filename$
    else
        FILTER$="PNG Files"+$0a$+"*.png"
        cfilename$ = FILESAVE("Save PNG File","","qrcode","png",filter$,MODE="CLIENT")
        if cfilename$(1,2)="::" then
            return
        fi
        gosub createpngfile        
        BBjAPI().getThinClient().getClientFileSystem().getClientFile(cfilename$).copyToClient(filename$)
    fi
     
return

createpngfile:
     filename$ = java.io.File.createTempFile("clip_",".png",null()).getAbsolutePath()
     ch=unt
     open (ch)filename$
     write (ch) bytes!
     close (ch)
return

generation_error:
    a=msgbox(errmes(-1),48,"Error")
goto byebye